#INCLUDE 'PROTHEUS.CH'

/*****************************************************************************/
/*/{Protheus.doc} AGRA045

@description Ponto de Etrada - Locais de Estoque

@author Bernard M. Margarido
@since 27/10/2018
@version 1.0

@type function
/*/
/*****************************************************************************/
User Function AGRA045()
Local aArea		:= GetArea()
Local aParam 	:= ParamIxb

Local cIdPonto	:= ""
Local cIdModel	:= ""

Local oModel	:= Nil
Local oModelNNR	:= Nil
 
If ValType(aParam) == "A"
	//---------------------------------+
	// Variaveis de validação do model |
	//---------------------------------+
	cIdPonto	:= aParam[2]
	cIdModel	:= aParam[3]
	
	//-----------------------------+
	// Apos a gravação do registro |
	//-----------------------------+ 
	If cIdPonto == "MODELCOMMITTTS"
		//-----------------------+
		// Carrega Objetos Model |
		//-----------------------+
		oModel		:= FwModelActive()
		oModelNNR	:= oModel:GetModel( 'NNRMASTER' )
		
		//-------------------------+
		// Alteração do Fornecedor |
		//-------------------------+
		If oModel:nOperation == 4
			dbSelectArea("NNR")
			NNR->( dbSetOrder(1) )
			If NNR->( dbSeek(xFilial("NNR") + oModelNNR:GetValue('NNR_CODIGO') ))
				RecLock("NNR",.F.)
					NNR->NNR_XDTALT := dDataBase 
					NNR->NNR_XHRALT := Time()
				NNR->( MsUnLock() )	
			EndIf	
		EndIf
	EndIf
	
EndIf
		
RestArea(aArea)			
Return .T.